apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {

    def secretProperties = getSecretProperties()

    compileSdkVersion 28
    flavorDimensions "default"
    defaultConfig {
        applicationId "br.com.cambit"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [
                backapp_server_url: secretProperties["BACKAPP_SERVER_URL"],
                backapp_app_id    : secretProperties["BACKAPP_APP_ID"],
                backapp_client_key: secretProperties["BACKAPP_CLIENT_KEY"]
        ]

        buildConfigField("String", "BACKAPP_SERVER_URL", "\"${secretProperties["BACKAPP_SERVER_URL"]}\"")
        buildConfigField("String", "BACKAPP_APP_ID", "\"${secretProperties["BACKAPP_APP_ID"]}\"")
        buildConfigField("String", "BACKAPP_CLIENT_KEY", "\"${secretProperties["BACKAPP_CLIENT_KEY"]}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }
}

def getSecretProperties() {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secret.properties')))
    return props
}

ext {
    coroutines_version = "1.0.1"
    koin_version = "1.0.2"

    androidx_version = "1.1.0-alpha02"
    androidX_support_version = '1.1.0-alpha02'
    constraint_layout_version = '2.0.0-alpha3'
    android_material_version = '1.1.0-alpha04'
    glide_version = '4.8.0'

    parse_server_version = "1.16.3"
}

dependencies {


    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.koin:koin-android:$koin_version"

    // UI
    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.recyclerview:recyclerview:$androidX_support_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "com.google.android.material:material:$android_material_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // NETWORKING
    implementation "com.parse:parse-android:$parse_server_version"

    //CARDVIEW
    implementation "androidx.cardview:cardview:1.0.0"
    testImplementation "junit:junit:4.12"

    androidTestImplementation "androidx.test:runner:1.1.2-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.2-alpha01"
}
